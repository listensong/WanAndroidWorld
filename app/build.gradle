apply plugin: "com.android.application"

apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

apply plugin: "jacoco"

def keystoreFilepath = ''
def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyfile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.song.example.study"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    signingConfigs {
        release {
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyfile
            storePassword keystorePSW
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            if (keyfile.exists()) {
                println("WITH -> buildTypes -> release: using jks key")
                signingConfig signingConfigs.release
            } else {
                println("WITH -> buildTypes -> release: using default key")
            }
        }
        debug {
            testCoverageEnabled true
            debuggable true
            if (keyfile.exists()) {
                println("WITH -> buildTypes -> debug: using jks key")
                signingConfig signingConfigs.release
            } else {
                println("WITH -> buildTypes -> debug: using default key")
            }
        }
    }

    buildFeatures {
        dataBinding {
            enabled true
        }
    }

    lintOptions {
        abortOnError false
        tasks.lint.enabled = false
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
//        androidTest {
//            assets.srcDirs += ['androidTest/assets/']
//        }
    }

    packagingOptions {
        exclude "META-INF/ASL2.0"
    }
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")

    implementation project(path: ":common")
    implementation project(path: ":wanandroid")

    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    kapt "androidx.room:room-compiler:$androidXRoomVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    //kapt "com.google.dagger:dagger-compiler:$googleDaggerVersion"

    testImplementation project(path: ":basetest")
    testImplementation project(path: ":basetest", configuration: 'unitTestDependencies')
    androidTestImplementation project(path: ":basetest", configuration: 'androidUnitTestDependencies')
}
